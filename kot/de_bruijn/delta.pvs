delta: THEORY
BEGIN
  IMPORTING subst, interp

  Γ: VAR finseq[(scontext?)]
  U, V: VAR syntaxic
  x: VAR nat
  σ: VAR finseq[(sinterp?)]
  n: VAR nat
  m: VAR (sexpr?)
  a: VAR (sexpr?)

  w: [finseq[(scontext?)], syntaxic -> ordinal]
  definition?(U): bool =
    theory_def?(U) OR const_def?(U) OR type_def?(U)
  var?(U): bool =
    const_var?(U) OR type_var?(U)
  ;rest[T: TYPE]: [finseq[T], nat -> finseq[T]]
  k: [(scontext?) -> finseq[(scontext?)]]
  ;++(s1, s2: finseq[(scontext?)]): finseq[(scontext?)] = s2 o s1

  wdef: AXIOM x < length(Γ) AND definition?(Γ(x)) IMPLIES
    w(rest(Γ, x), def(Γ(x))) < w(Γ, v(x))
  wdot: AXIOM
    w(Γ, m) < w(Γ, dot(m, a))

  η(m, n)(U): RECURSIVE same(U) =
    IF n = 0 THEN U
    ELSE η(m, n - 1)(subst(U, n - 1, dot(m ↑ (n - 1), v(n - 1)))) ENDIF
  MEASURE n
  AUTO_REWRITE+ sexpr?, scontext?, sinterp?
  η(m, n)(l: list[(sexpr?)]): RECURSIVE list[(sexpr?)] =
    CASES l OF
      null: null
    , cons(h, t): cons(η(m, n)(h), η(m, n)(t))
    ENDCASES
  MEASURE length(l)

  δ(Γ)(U): RECURSIVE same(U) =
    CASES U OF
      v(x):
        IF x < length(Γ) AND definition?(Γ(x))
	  THEN δ(rest(Γ, x))(def(Γ(x)))
	  ELSE v(x)
	ENDIF
    , dot(m, x):
        CASES δ(Γ)(m) OF
	  theory_(Θ):
	    IF v?(x) AND i(x) < length(Θ) THEN
	      IF definition?(Θ(i(x)))
	        THEN δ(Γ)(η(m, length(Θ) - i(x))(def(Θ(i(x)))))
	      ELSIF var?(Θ(i(x)))
	        THEN dot(m, x)
	      ELSE dot(theory_(Θ), x)
	      ENDIF
	    ELSE dot(theory_(Θ), x)
	    ENDIF
	  ELSE dot(δ(Γ)(m), x)
	ENDCASES
    , interp(m, σ):
        CASES δ(Γ)(m) OF
	  theory_(Θ): δ(Γ)(theory_(interpret(Θ, σ)))
	  ELSE interp(δ(Γ)(m), σ)
	ENDCASES
    , theory_(Θ):
        theory_(Θ WITH [ `seq := LAMBDA (x: below[length(Θ)]): δ(Γ ++ rest(Θ, x))(Θ(x)) ])

    , fun(A, B): fun(δ(Γ)(A), δ(Γ ++ k(const_var(δ(Γ)(A))))(B))
    , prod(A, B): prod(δ(Γ)(A), δ(Γ ++ k(const_var(δ(Γ)(A))))(B))
    , subtype(T, a): subtype(δ(Γ)(T), a)

    , type_decl: type_decl
    , type_var: type_var
    , type_def(T): type_def(δ(Γ)(T))
    , const_decl(T): const_decl(δ(Γ)(T))
    , const_var(T): const_var(δ(Γ)(T))
    , const_def(T, a): const_def(δ(Γ)(T), a)
    , theory_def(Th): theory_def(δ(Γ)(Th))

      ELSE U
    ENDCASES
  MEASURE w(Γ, U)
  AUTO_REWRITE- sexpr?, scontext?, sinterp?
END delta
