language: THEORY
BEGIN
  syntaxic: DATATYPE
  BEGIN
    v(i: nat): v?
    dot(m: nat, map: syntaxic, s: syntaxic): dot?
  
    fun(dom: syntaxic, range: syntaxic): fun?
    prod(left: syntaxic, right: syntaxic): prod?
    subtype(supertype: syntaxic, pred: syntaxic): subtype?

    app(op: syntaxic, arg: syntaxic): app?
    lam(type_: syntaxic, body: syntaxic): lam?
    pair(left: syntaxic, right: syntaxic): pair?
    lproj(arg: syntaxic): lproj?
    rproj(arg: syntaxic): rproj?

    type_decl: type_decl?
    type_var: type_var?
    type_def(def: syntaxic): type_def?
    const_decl(type_: syntaxic): const_decl?
    const_var(type_: syntaxic): const_var?
    const_def(type_: syntaxic, def: syntaxic): const_def?
    theory_def(def: syntaxic): theory_def?

    nil: nil?
    itype(rest: syntaxic, i: nat, def: syntaxic): itype?
    iconst(rest: syntaxic, i: nat, def: syntaxic): iconst?
%    itheory(def: syntaxic): itheory?

    map(run_map: finseq[syntaxic]): map?
  END syntaxic

  prekeyword: TYPE =
    { PRETYPE, PRETERM, PRECONTEXT, PREINTERP, PREDECL }

  U, V: VAR syntaxic
  x, y: VAR nat
  i, j, n: VAR nat
  k: VAR prekeyword

  sinterp?(U): RECURSIVE bool =
    CASES U OF
      nil: TRUE
    , itype(τ, i, T): sinterp?(τ)
    , iconst(τ, i, a): sinterp?(τ)
      ELSE FALSE
    ENDCASES
  MEASURE U BY <<

  sdecl?(U): bool =
    type_decl?(U) OR type_var?(U) OR type_def?(U) OR const_decl?(U) OR
    const_var?(U) OR const_def?(U) OR theory_def?(U)

  scontext?(U): bool =
    map?(U) AND FORALL (x: below[run_map(U)`length]): sdecl?(run_map(U)`seq(x))

  k(d: (sdecl?)): (scontext?) =
    map((# length := 1, seq := LAMBDA (x: below[1]): d #))

  M, N: VAR (scontext?)
  empty: (scontext?) = map(empty_seq)

  length(M): MACRO nat =
    run_map(M)`length
  ;∈(x, M): MACRO bool =
    x < length(M)
  apply(M)(x | x ∈ M): (sdecl?) =
    run_map(M)`seq(x)
  CONVERSION+ apply
  rest(M, (x | x < length(M))): (scontext?) =
    map(run_map(M) ^^ (0, length(M) - x))
  ;++(M, N): (scontext?) =
    map(run_map(N) o run_map(M))

  % TODO: move
  sum(len: nat, f: [below[len] -> nat]): RECURSIVE nat =
    IF len = 0 THEN 0
    ELSE f(len - 1) + sum(len - 1, LAMBDA (x: below[len - 1]): f(x)) ENDIF
  MEASURE len

  size(U): RECURSIVE nat =
    CASES U OF
      v(i): 2
    , dot(i, σ, x): size(σ) + 2

    , fun(A, B): size(A) + size(B) + 2
    , prod(A, B): size(A) + size(B) + 2
    , subtype(T, a): size(T) + size(a) + 2

    , app(f, a): size(f) + size(a) + 2
    , lam(T, a): size(T) + size(a) + 2
    , pair(a, b): size(a) + size(b) + 2
    , lproj(a): size(a) + 2
    , rproj(a): size(a) + 2

    , type_decl: 1
    , type_var: 1
    , type_def(T): size(T) + 1
    , const_decl(T): size(T) + 1
    , const_var(T): size(T) + 1
    , const_def(T, a): size(T) + size(a) + 1
    , theory_def(Θ): size(Θ) + 1

    , nil: 0
    , itype(τ, i, T): size(τ) + size(T) + 4
    , iconst(τ, i, a): size(τ) + size(a) + 4

    , map(f): sum(f`length, LAMBDA (x: below[f`length]): 1 + size(f(x)))
    ENDCASES
  MEASURE U BY <<
  AUTO_REWRITE size
  prefix_size: THEOREM FORALL (f, g: finseq[syntaxic]):
    f`length < g`length AND (FORALL (x: below[f`length]): f`seq(x) = g`seq(g`length - f`length + x)) IMPLIES
      sum(f`length, LAMBDA (x: below[f`length]): 1 + size(f(x))) < sum(g`length, LAMBDA (x: below[g`length]): 1 + size(g(x)))

  kind?(U, k): bool = 
    CASES U OF
      type_decl: k = PRETYPE
    , type_var: k = PRETYPE
    , type_def(T): k = PRETYPE
    , const_decl(T): k = PRETERM
    , const_var(T): k = PRETERM
    , const_def(T, a): k = PRETERM
    , theory_def(Θ): k = PRECONTEXT
    ELSE FALSE
    ENDCASES
  AUTO_REWRITE kind?

  interpret?(U, i): RECURSIVE bool =
    CASES U OF
      itype(τ, j, T): i = j OR interpret?(τ, i)
    , iconst(τ, j, a): i = j OR interpret?(τ, i)
    ELSE FALSE
    ENDCASES
  MEASURE U BY <<

  compatible?(M, U): RECURSIVE bool =
    CASES U OF
      nil: TRUE
    , itype(τ, i, T):
        i < length(M) AND type_decl?(M(i)) AND NOT interpret?(τ, i) AND compatible?(M, τ)
    , iconst(τ, i, a):
        i < length(M) AND const_decl?(M(i)) AND NOT interpret?(τ, i) AND compatible?(M, τ)
    ELSE FALSE
    ENDCASES
  MEASURE U BY <<
  AUTO_REWRITE compatible?
  compatible_interp: THEOREM
    length(M) = length(N) AND
    (FORALL (x: below[length(M)]): x /= i => M(x) = N(x)) AND
    (compatible?(M, itype(U, i, V)) OR compatible?(M, iconst(U, i, V)))
      IMPLIES compatible?(N, U)

  interp(M, (U | compatible?(M, U))): RECURSIVE (scontext?) =
    CASES U OF
      nil: M
    , itype(τ, i, T): interp(map(run_map(M) WITH [ `seq(i) := type_def(T) ]), τ)
    , iconst(τ, i, a): interp(map(run_map(M) WITH [ `seq(i) := const_def(type_(M(i)), a) ]), τ)
    ENDCASES
  MEASURE U BY <<

  prekind?(M: (scontext?))(U)(k): RECURSIVE bool =
    CASES U OF
      v(i): (k = PRETYPE OR k = PRETERM) AND i < length(M) AND kind?(M(i), k)
    , dot(i, σ, x): (k = PRETYPE OR k = PRETERM) AND
        i < length(M) AND prekind?(M)(σ)(PREINTERP) AND v?(x) AND
	kind?(M(i), PRECONTEXT) AND (type_def?(M(i)) OR const_def?(M(i))) AND
	compatible?(def(M(i)), σ) AND prekind?(def(M(i)))(x)(k)

    , fun(A, B): k = PRETYPE AND prekind?(M)(A)(PRETYPE) AND prekind?(M)(B)(PRETYPE)
    , prod(A, B): k = PRETYPE AND prekind?(M)(A)(PRETYPE) AND prekind?(M)(B)(PRETYPE)
    , subtype(T, a): k = PRETYPE AND prekind?(M)(T)(PRETYPE) AND prekind?(M)(a)(PRETERM)

    , app(f, a): k = PRETERM AND prekind?(M)(f)(PRETERM) AND prekind?(M)(a)(PRETERM)
    , lam(T, a): k = PRETERM AND prekind?(M)(T)(PRETYPE) AND prekind?(M)(a)(PRETERM)
    , pair(a, b): k = PRETERM AND prekind?(M)(a)(PRETERM) AND prekind?(M)(b)(PRETERM)
    , lproj(a): k = PRETERM AND prekind?(M)(a)(k)
    , rproj(a): k = PRETERM AND prekind?(M)(a)(k)

    , type_decl: k = PREDECL
    , type_var: k = PREDECL
    , type_def(T): k = PREDECL AND prekind?(M)(T)(PRETYPE)
    , const_decl(T): k = PREDECL AND prekind?(M)(T)(PRETYPE)
    , const_var(T): k = PREDECL AND prekind?(M)(T)(PRETYPE)
    , const_def(T, a) :
        k = PREDECL AND prekind?(M)(T)(PRETYPE) AND prekind?(M)(a)(PRETERM)
    , theory_def(Θ):
        k = PREDECL AND prekind?(M)(Θ)(PRECONTEXT)

    , nil: k = PREINTERP
    , itype(σ, i, T): k = PREINTERP AND prekind?(M)(T)(PRETYPE) AND prekind?(M)(σ)(PREINTERP)
    , iconst(σ, i, a): k = PREINTERP AND prekind?(M)(a)(PRETERM) AND prekind?(M)(σ)(PREINTERP)

    , map(f):
        k = PRECONTEXT AND scontext?(U) AND
	FORALL (x: below[f`length]): prekind?(M ++ rest(U, x))(f`seq(x))(PREDECL)
    ENDCASES
  MEASURE U BY <<
  AUTO_REWRITE prekind?

  precontext?(U): MACRO bool = prekind?(empty)(U)(PRECONTEXT)
  precontext: TYPE = (precontext?)

  Γ: VAR precontext

  pretheory?(Γ)(U): MACRO bool = prekind?(Γ)(U)(PRECONTEXT)
  pretype?(Γ)(U): MACRO bool = prekind?(Γ)(U)(PRETYPE)
  preterm?(Γ)(U): MACRO bool = prekind?(Γ)(U)(PRETERM)
  preinterp?(Γ)(U): MACRO bool = prekind?(Γ)(U)(PREINTERP)
  semantic?(Γ)(U): bool =
    pretheory?(Γ)(U) OR pretype?(Γ)(U) OR preterm?(Γ)(U) OR preinterp?(Γ)(U)

  pretheory(Γ): TYPE = (pretheory?(Γ))
  pretype(Γ): TYPE = (pretype?(Γ))
  preterm(Γ): TYPE = (preterm?(Γ))
  preinterp(Γ): TYPE = (preinterp?(Γ))
  semantic(Γ): TYPE = (semantic?(Γ))

  precontext_is_map: JUDGEMENT precontext SUBTYPE_OF (map?)
  pretheory_is_map: JUDGEMENT pretheory(Γ) SUBTYPE_OF (map?)
  precontext_is_scontext: JUDGEMENT precontext SUBTYPE_OF (scontext?)
  pretheory_is_scontext: JUDGEMENT pretheory(Γ) SUBTYPE_OF (scontext?)
END language
