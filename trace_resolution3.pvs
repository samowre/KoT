% m = number of vars
% n = number of lines in the certificate
% i = number of initial clauses

trace_resolution3[m, n: nat, i : upto(n)]: THEORY
BEGIN

  IMPORTING minlist


  tr_literal?(k: nzint): bool = (abs(k) < m)

  member_numpred_nzint :  LEMMA 
    FORALL(k: nzint, kk: list[nzint]): 
      member[numfield](k, kk) = member[nzint](k, kk)

  subset_numpred_nzint :  LEMMA 
    FORALL(kk: list[nzint], ll: list[nzint]): 
      subset[numfield](kk, ll) = subset[nzint](kk, ll)

  k, l: VAR (tr_literal?)
  kk, ll: VAR list[(tr_literal?)]

  tr_neg(k): (tr_literal?) = -k

  member_tr_lit_member_nzint: LEMMA 
    member[nzint](k, ll) = member[(tr_literal?)](k, ll)
  
  subset_tr_lit_subset_nzint: LEMMA 
    subset[nzint](kk, ll) = subset[(tr_literal?)](kk, ll)

  % (-1) -> 1, (1) -> 2, (-2) -> 3, (2) -> 4 ...
  index(k): posnat = IF (k<0) THEN 2*abs(k)-1 ELSE 2*k ENDIF

  index_inj: LEMMA injective?(index)

  %index must be injective?
  IMPORTING sortedlist[(tr_literal?), index]

  % A trace clause is a list of trace literals sorted by index
  tr_clause?(ll): bool = sorted?(ll)

  tr_clause_prop: LEMMA tr_clause?(ll) IFF 
    		       (null?(ll) 
		        OR null?(cdr(ll)) 
			OR (index(car(ll))<index(car(cdr(ll))) 
			AND tr_clause?(cdr(ll))))

  ck, cl, cm: VAR (tr_clause?)

  cons_tr_clause: LEMMA 
    (tr_clause?(ck) AND cons?(ck)) 
    IMPLIES tr_clause?(cdr(ck))

  %not empty trace clause
  ne_tr_clause?(cl): bool = NOT null?(cl)
 
  tr_clause_true?(cl): RECURSIVE bool = 
    CASES cl OF
      null: FALSE,
      cons (l, ck): sorted_contains(tr_neg(l), ck) OR tr_clause_true?(ck)
    ENDCASES
  MEASURE length(cl)

  clause_true_lemma :  LEMMA
        tr_clause_true?(cl) = 
	EXISTS l: member(l, cl) and member(tr_neg(l), cl)
 
  ncl, nck, ncm : VAR (ne_tr_clause?)

  %list of integers smaller than order
  chain?(order:upto(n))( l:list[below(n)]) : recursive bool = 
    if null?(l) then true
    else car(l) < order and
    	 chain?(order)(cdr(l))	
    endif
  measure length(l)

  list_of_chains_offset?(order: upto(n))( l: list[list[below(n)]]): recursive boolean =
    if null?(l) then true
    else chain?(order)( car(l) ) and 
         list_of_chains_offset?(order+1)( cdr(l) )
    endif
  measure length(l)
  
  list_of_chains?(l: list[list[below(n)]]): boolean = 
    list_of_chains_offset?(0)(l) and length(l) = n

  array_of_clauses :TYPE = ARRAY[below(n) -> (tr_clause?)]

  % tr_clause is sorted by index => ~p, p are on consecutive positions.
  % deletes the pair (~pivot, pivot)  
  delete_pivot(ck, (pivot: {l| member(l, ck) AND member(tr_neg(l), ck)}))
   : {cl | FORALL l: 
     	   member(l, cl) <=> 
	   member(l, ck) 
	     AND (NOT l = pivot) 
	     AND (NOT l = tr_neg(pivot))} = 
    remove(tr_neg(pivot), remove(pivot, ck))

  % true if a pivot exists for ck and cl
  exist_pivot?(ck, cl) : RECURSIVE bool =
    CASES ck OF 
      null: FALSE,
      cons(k, cm): member(tr_neg(k), cl) OR exist_pivot?(cm, cl)
    ENDCASES
  MEASURE length(ck)

  exist_lemma : LEMMA
    exist_pivot?(ck, cl) <=> EXISTS k: member(k, ck) AND member(tr_neg(k), cl)

  % returns a pivot of nck and ncl
  find_pivot(nck, (ncl:{ncm | exist_pivot?(nck, ncm)})) : 
   RECURSIVE {k | member(k, nck) AND member(tr_neg(k), ncl)} =
    CASES cdr(nck) OF
      null: car(nck),
      cons(k, ck): 
        IF member(tr_neg(car(nck)), ncl) 
	THEN car(nck) 
	ELSIF member(tr_neg(k), ncl) 
	THEN k 
	ELSE find_pivot(cdr(nck), ncl) 
	ENDIF
    ENDCASES
  MEASURE length(nck)
      
  %eliminates duplicates
  merge(ck, cl) : RECURSIVE 
       {cla: (tr_clause?) | FORALL k: 
       	     	 member(k,cla)  
		 IFF (member(k,ck) OR member(k,cl))} = 
       	     CASES ck OF
   	         null : cl,
		 cons(k, ckk) : 
	  	  CASES cl OF
	    	  	null : ck,
	    		cons(l, cll) : IF k = l
				        THEN cons(k, merge(ckk, cll))
					ELSIF index(k) < index(l) 
	          	   	        THEN cons(k, merge(ckk, cl))
			    		ELSE cons(l, merge(ck, cll))
			  	       ENDIF
	         ENDCASES
             ENDCASES
      	     MEASURE length(ck) + length(cl)

  merge_includes1: LEMMA
    sorted_include(ck, merge(ck, cl)) AND sorted_include(cl, merge(ck, cl))
    
  % applies resolution to nck and ncl
  resolution(nck, ncl) : (tr_clause?) = 
    IF tr_clause_true?(nck) THEN ncl
    ELSIF tr_clause_true?(ncl) THEN nck
    ELSE
     LET merged = merge(nck, ncl) IN
     IF exist_pivot?(nck, ncl) THEN
       LET pivot= find_pivot(nck, ncl) IN 
       delete_pivot(merged, pivot) 
     ELSE
       merged
     ENDIF
    ENDIF

  not_true_clauses(ck, cl): bool = 
    NOT tr_clause_true?(ck) 
    AND NOT tr_clause_true?(cl)

  % member
  % member(x, ck) => member(x, res) \/ x =  pivot
  % member(x, cl) => member(x, res) \/ x = -pivot
  resolution_lemma : LEMMA
    not_true_clauses(nck, ncl) => 
    LET res = resolution(nck, ncl) IN
    IF exist_pivot?(nck, ncl) THEN
      LET pivot = find_pivot(nck, ncl) IN
      (FORALL k: member(k, nck) => member(k, res) OR k=pivot)
       AND
        FORALL l: member(l, ncl) => member(l, res) OR l= tr_neg(pivot)
    ELSE
      sorted_include(nck, res) AND sorted_include(ncl, res)
    ENDIF

  include_res: LEMMA
    not_true_clauses(nck, ncl) => 
    LET res = resolution(nck, ncl) IN
    exist_pivot?(nck, ncl) 
     => 
      LET pivot = find_pivot(nck, ncl) IN 
      subset(nck, cons(pivot, res))&
      subset(ncl, cons(tr_neg(pivot), res))
    
      
  %applies merge on a non-empty list of clauses
  resolution_list(lck: (cons?[(tr_clause?)])): RECURSIVE(tr_clause?) =
     CASES cdr(lck) OF
       null: car(lck),
       cons(ck, lcl) : IF ne_tr_clause?(car(lck)) AND ne_tr_clause?(ck) 
       		        THEN resolution_list(
			       cons(resolution(car(lck), ck), 
			       lcl))
			ELSE null
			ENDIF
     ENDCASES 
     MEASURE length(lck)

  % applies resolution step on acc and first element of l
  new_resolution_list_rec( acc: (tr_clause?), 
  			   cl_array: array_of_clauses, 
			   j: below(n), 
			   ch: (chain?(j))): recursive (tr_clause?) = 
    if null?(ch)
    then
      acc
    else 
      new_resolution_list_rec( resolution(acc, cl_array(car(ch))), cl_array, j, cdr(ch))
    endif
  measure length(ch)

  % returns the clause obtained by chain resolution on c(j)`chn
  new_resolution_list( cl_array:array_of_clauses, j: below(n), ch: {c: (chain?(j))| cons?(c)} ): (tr_clause?) =	
    let acc= cl_array( car(ch) ) in 
    if null?(cdr(ch)) 
    then
      acc
    else
      new_resolution_list_rec(acc, cl_array, j, cdr(ch))
    endif

  
  		    
  % applies resolution_list on all chains starting from j  
  resolve_rec( j: upto(n), 
  	       chain_list:(list_of_chains_offset?(j)),
  	       clause_array: array_of_clauses): RECURSIVE array_of_clauses = 
    IF j=n 
    % finished all the steps
    THEN clause_array
    ELSIF null?(car(chain_list))
    % no chain resolution needed
    THEN resolve_rec(j+1, cdr(chain_list), clause_array)
    % applies chain resolution
    ELSE resolve_rec( j+1, cdr(chain_list), 
    	 	      clause_array WITH [(j) := 
		        new_resolution_list(clause_array, j, car(chain_list))])
    ENDIF 
  MEASURE n-j

  resolve(ch_list : (list_of_chains?), cl_array: array_of_clauses): array_of_clauses = 
    resolve_rec(0, ch_list, cl_array)
 
END trace_resolution3
